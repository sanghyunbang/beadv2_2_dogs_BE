plugins {
    id 'java'
    id 'org.springframework.boot' version '4.0.0' apply false
    id 'io.spring.dependency-management' version '1.1.7' apply false
    id 'com.diffplug.spotless' version '7.0.2' apply false
    id 'checkstyle'
}

allprojects {
    group = 'com.barofarm'
    version = '0.0.1-SNAPSHOT'

    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'com.diffplug.spotless'
    apply plugin: 'checkstyle'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(21)
            vendor = JvmVendorSpec.JETBRAINS
        }
    }

    ext {
        set('springCloudVersion', "2025.0.0")
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        }
    }

    dependencies {
        // Lombok
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'

        // Test
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    }

    tasks.named('test') {
        useJUnitPlatform()
    }

    // ============================================
    // Spotless - 코드 자동 포맷팅 (like Python black)
    // ============================================
    spotless {
        java {
            target 'src/*/java/**/*.java'
            importOrder()
            removeUnusedImports()
            googleJavaFormat('1.25.2')
            trimTrailingWhitespace()
            endWithNewline()
        }
    }

    // ============================================
    // Checkstyle - 코드 스타일 검사 (lint)
    // ============================================
    checkstyle {
        toolVersion = '10.21.4'
        configFile = rootProject.file('config/checkstyle/checkstyle.xml')
        ignoreFailures = false
        maxWarnings = 0
    }

    tasks.withType(Checkstyle).configureEach {
        reports {
            xml.required = true
            html.required = true
        }
    }
}

// ============================================
// 커밋 전 검사용 Task
// ============================================
tasks.register('lint') {
    group = 'verification'
    description = '코드 품질 검사 (spotless + checkstyle)'
    dependsOn subprojects.collect { it.tasks.matching { t -> t.name == 'spotlessCheck' } }
    dependsOn subprojects.collect { it.tasks.matching { t -> t.name == 'checkstyleMain' } }
}

tasks.register('format') {
    group = 'verification'
    description = '코드 자동 포맷팅'
    dependsOn subprojects.collect { it.tasks.matching { t -> t.name == 'spotlessApply' } }
}

// 서비스 모듈은 jar만 생성 (라이브러리로 사용)
[
    ':baro-auth:auth-service',
    ':baro-buyer:buyer-service', ':baro-buyer:cart-service', ':baro-buyer:product-service',
    ':baro-seller:seller-service', ':baro-seller:farm-service',
    ':baro-order:order-service', ':baro-order:payment-service',
    ':baro-support:settlement-service', ':baro-support:delivery-service', ':baro-support:notification-service',
    ':baro-support:experience-service', ':baro-support:search-service', ':baro-support:review-service',
    ':baro-cloud:gateway-service', ':baro-cloud:config-server', ':baro-cloud:eureka-server'
].each { servicePath ->
    project(servicePath) {
        bootJar.enabled = false
        jar.enabled = true
    }
}
